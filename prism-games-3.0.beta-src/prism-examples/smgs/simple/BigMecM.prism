smg

const int N;

const int M=2*((2*(N) +1));

global state: [0..4] init 0;
global m: [0..M] init 0;

player P1 player1, [a11], [a12] endplayer
player P2 player2, [a21], [a22] endplayer

module player1
	i: [0..N] init 0;

	//go back
	[a11] state=1 & i=0 & m=0 -> 1/2 : (state'=0) + 1/2 : (state'=0) & (m'=1);
	[a11] state=1 & i>0 & m=0 -> 1/2 : (i'=i-1) + 1/2 : (i'=i-1) & (m'=1);
	[a11] state=2 & i=0 & m=0 -> 1/2 : (state'=0) + 1/2 : (state'=0) & (m'=1);
	[a11] state=2 & i>0 & m=0 -> 1/2 : (i'=i-1) + 1/2 : (i'=i-1) & (m'=1);
	
	//go forward
	[a12] state=1 & i<N-1 & m=0 -> 1/2 : (i'=i+1) + 1/2 : (i'=i+1) & (m'=1);
	[a12] state=2 & i<N-1 & m=0 -> 1/2 : (i'=i+1) + 1/2 : (i'=i+1) & (m'=1);

	[] m>0 & m<M & !(state=0 | state=4) -> 1/2 : (m'=0) + 1/2 : (m'=m+1);
	[] m=M & !(state=0 | state=4) -> 1/2 : (m'=0) + 1/2 : (state'=4);

	//try to win
	[a12] state=1 & i=N-1 & m=0 -> 1/2 : (state'=3) + 1/2 : (state'=4);
	[a12] state=2 & i=N-1 & m=0 -> 2/5 : (state'=3) + 3/5 : (state'=4);

	[] state=3 -> (state'=3);

endmodule

module player2	
	[a21] state=0 & m=0 -> 1/2 : (state'=1) + 1/2 : (m'=1) & (state'=1);
	[a22] state=0 & m=0 -> 1/2 : (state'=2) + 1/2 : (m'=1) & (state'=2);
	[] state=4 -> (state'=4);
	
	[] m>0 & m<M & (state=0 | state=4) -> 1/2 : (m'=0) + 1/2 : (m'=m+1);
	[] m=M & (state=0 | state=4) -> 1/2 : (m'=0) + 1/2 : (state'=4);

endmodule

//Labels
formula done = state=3 | state=4;
label "done" = done;
label "p1win" = done & state=3;
label "p2win" = done & state=4;
